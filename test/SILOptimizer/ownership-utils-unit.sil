// RUN: %target-sil-opt -unit-test-runner %s -o /dev/null 2>&1 | %FileCheck %s

sil_stage canonical

import Builtin

class C {}

sil @getOwned : $@convention(thin) () -> (@owned C)

sil @borrow : $@convention(thin) (@guaranteed C) -> ()

sil @useUnmanaged : $@convention(thin) (@sil_unmanaged C) -> ()

sil [ossa] @test_escape_of_phi_transitive_incoming_value : $@convention(thin) () -> () {
entry:
  %getOwned = function_ref @getOwned : $@convention(thin) () -> (@owned C)
  %useUnmanaged = function_ref @useUnmanaged : $@convention(thin) (@sil_unmanaged C) -> ()
  cond_br undef, left, right
left:
  cond_br undef, left_left, left_right
left_left:
  %llinstance = apply %getOwned() : $@convention(thin) () -> (@owned C)
  br left_bottom(%llinstance : $C)
left_right:
  %lrinstance = apply %getOwned() : $@convention(thin) () -> (@owned C)
  %escape = ref_to_unmanaged %lrinstance : $C to $@sil_unmanaged C
  apply %useUnmanaged(%escape) : $@convention(thin) (@sil_unmanaged C) -> ()
  br left_bottom(%lrinstance : $C)
left_bottom(%linstance : @owned $C):
  br exit(%linstance : $C)
right:
  %rinstance = apply %getOwned() : $@convention(thin) () -> (@owned C)
  br exit(%rinstance : $C)
exit(%instance : @owned $C):
// CHECK-LABEL: begin running test {{[0-9]+}} of {{[0-9]+}} on test_escape_of_phi_transitive_incoming_value: has-pointer-escape
// CHECK:       %14 = argument of bb6 : $C
// CHECK:       true
// CHECK-LABEL: end running test {{[0-9]+}} of {{[0-9]+}} on test_escape_of_phi_transitive_incoming_value: has-pointer-escape with
  test_specification "has-pointer-escape @block.argument"
  destroy_value %instance : $C
  %retval = tuple ()
  return %retval : $()
}
